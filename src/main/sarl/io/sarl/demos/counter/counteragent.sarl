package io.sarl.demos.counter

import io.sarl.core.Behaviors
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.core.Schedules
import java.util.concurrent.atomic.AtomicInteger
import io.sarl.core.Logging

event Hello

event Count

event Die

agent CounterAgent {

	uses Lifecycle, Schedules, DefaultContextInteractions, Behaviors, Logging

	val count = new AtomicInteger

	var started = false
 
	on Initialize {
		loggingName = "COUNTER"
		println("Starting")
		task("discovery").every(1000) [ 
			emit(new Hello)
		]
	}

	on Hello [ occurrence.source != defaultAddress && !started ] {
		println("Found another agent => " + occurrence.source)
		println("I'm " + defaultAddress)
		started = true
		task("discovery").cancel
		println("Starting to count")
		wake(new Count) 
	}

	on Count {
		println("count => " + count.get) 
		if(count.get == 3) {
			println("Killing") 
			emit(new Die) 
			killMe
		} else {
			in(1000) [ 
				count.incrementAndGet 
				wake(new Count) 
			]
		}
	}

}

agent SecondAgent {

	uses Lifecycle, DefaultContextInteractions, Logging
	
	on Initialize {
		loggingName = "PARTNER"
		println("Starting")
	}

	on Hello [ occurrence.source != defaultAddress ] {
		println("Say hello!")
		emit(new Hello) 
	}

	on Die {
		println("Killing")
		killMe
	}

}
