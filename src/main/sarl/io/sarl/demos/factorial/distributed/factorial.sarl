package io.sarl.demos.factorial.distributed

import io.sarl.core.Behaviors
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.core.Schedules

event Factorial {
	var number : Integer
	var value : Integer
} 

event Calculate { 
	var number : Integer 
} 

event ComputationDone { 
	var result : Integer 
}

event Hello

event Die

capacity Logging {	
	def debug(s: String)
	def info(s: String)
}

skill BasicConsoleLogging implements Logging {
	
	val label : String

	new(label : String) {
		this.label = label
	}

	def debug(s: String) {
		println("DEBUG("+label+"): "+s)
	}
	
	def info(s: String) {
		println("INFO("+label+"): "+s)
	}
}

agent FactorialQueryAgent {
	uses Lifecycle, Behaviors, Logging, DefaultContextInteractions, Schedules
	
	var factValue : int

	var started = false
	
	on Initialize {
		factValue = new Integer(occurrence.parameters.get(0) as String)
		setSkill(Logging,new BasicConsoleLogging("Query"));

		info("Auto-detecting Factorial")
		task("discovery-task").every(1000) [
			emit(new Hello)
		]
	}

	on Hello [occurrence.source != defaultAddress && !started] {
		started = true
		task("discovery-task").cancel
		info("Found Factorial")
		info("Query sent with number "+factValue)
		emit(new Calculate=>[number=factValue])
	}
		
	on ComputationDone {
		info(factValue+"! = "+occurrence.result)
		emit(new Die)
		info("Killing")
		killMe
	}
}

agent FactorialAgent {
	uses Lifecycle, Behaviors, Logging, DefaultContextInteractions
	var upto : Integer
	  
	on Initialize {		
		setSkill(Logging,new BasicConsoleLogging("Factorial"));
		info("Factorial initialized")	
	} 

	on Hello [occurrence.source != defaultAddress] {
		emit(new Hello)
	}

	on Factorial [occurrence.number < upto ] {		
		wake(new Factorial => [ number = occurrence.number + 1 ; value = occurrence.value * (occurrence.number+1) ])
	}

	on Factorial [ occurrence.number == upto ] {  
		info(this.upto+"! = "+occurrence.value)	
		emit(new ComputationDone=>[result=occurrence.value])	
	}
	
	on Calculate {
		this.upto  = new Integer(occurrence.number);
		info("Query for " + this.upto + "!")
		wake(new Factorial => [ number = 0 ; value = 1 ])
	}

	on Die {
		info("Killing")
		killMe
	}

}

