package io.sarl.demos.basic

import io.sarl.core.Initialize
import io.sarl.core.Destroy

import io.sarl.core.Lifecycle
import io.sarl.core.InnerContextAccess
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Schedules
import io.janusproject.kernel.bic.MicroKernelCapacity
import io.janusproject.services.ContextSpaceService
import io.sarl.lang.core.AgentContext
import io.sarl.lang.core.Agent

event HolarchyDestroyed

agent HolarchyManager {
	
	uses Lifecycle, Schedules, DefaultContextInteractions

	on Initialize {
		spawn(Holon, #[])
	}
	
	on HolarchyDestroyed {
		System.out.println("Holarchy destroyed. Waiting awhile to ensure that all pending context destruction is processed.")
		in(5000) [
			var s = getSkill(MicroKernelCapacity)
			var serv = s.getService(ContextSpaceService)
			System.out.println("Registered contexts are: "+serv.contexts)
			for(context : serv.contexts) {
				System.out.println("Space in context "+context.ID
					+":" + context.spaces)
			}
			killMe
		]
	}
	
}

agent Holon{
	
	uses Lifecycle, InnerContextAccess, Schedules, DefaultContextInteractions
	
	val depth = 2
	val childCount = 1
	
	var isRoot : boolean
	
	on Initialize {
		var count : Integer
		if (occurrence.parameters.isEmpty) {
			count = depth
			isRoot = true
		}
		else {
			isRoot = false
			count = occurrence.parameters.get(0) as Integer
			if (count==null) count = 0
		}
		
 		if (isRoot || count > 0) {
			count = count - 1
			for(i : 1..childCount) {
				System.out.println("Create the child #"+i+" at level "+count)
				spawnInContext(Holon, innerContext, #[count])
			}
			every(1000) [
				if (innerContext.defaultSpace.participants.size == 1) {
					System.out.println("No more child: I'm killing myself.")
					killMe
				}
			]
		}
		else {
			System.out.println("As leaf: Waiting awhile before commiting a suicide.")
			in(10000) [
				System.out.println("As leaf: I'm killing myself.")
				killMe
			]
		}
	}
	
	on Destroy {
		if (isRoot) {
			emit(new HolarchyDestroyed)
		}
	}
		
}
