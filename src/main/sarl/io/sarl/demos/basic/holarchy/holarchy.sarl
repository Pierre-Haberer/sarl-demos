package io.sarl.demos.basic.holarchy

import io.janusproject.kernel.bic.MicroKernelCapacity
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import io.sarl.core.InnerContextAccess
import io.sarl.core.Lifecycle
import io.sarl.core.Schedules
import io.janusproject.services.contextspace.ContextSpaceService
import io.sarl.core.Logging

event HolarchyDestroyed

agent HolarchyManager {
	
	uses Lifecycle, Schedules, DefaultContextInteractions, Logging

	on Initialize {
		spawn(Holon, #[])
	}
	
	on HolarchyDestroyed {
		println("Holarchy destroyed. Waiting awhile to ensure that all pending context destruction is processed.")
		in(5000) [
			var s = getSkill(MicroKernelCapacity)
			var serv = s.getService(ContextSpaceService)
			println("Registered contexts are: "+serv.contexts)
			for(context : serv.contexts) {
				println("Space in context "+context.ID
					+":" + context.spaces)
			}
			killMe
		]
	}
	
}

agent Holon{
	
	uses Lifecycle, InnerContextAccess, Schedules, DefaultContextInteractions, Logging
	
	val depth = 3
	val childCount = 2
	
	var isRoot : boolean
	
	on Initialize {
		var count : Integer
		if (occurrence.parameters.isEmpty) {
			count = depth
			isRoot = true
		}
		else {
			isRoot = false
			count = occurrence.parameters.get(0) as Integer
			if (count==null) count = 0
		}
		
 		if (isRoot || count > 0) {
			count = count - 1
			for(i : 1..childCount) {
				println("Create the child #"+i+" at level "+count)
				spawnInContext(Holon, innerContext, #[count])
			}
			every(1000) [
				if (innerContext.defaultSpace.participants.size == 1) {
					println("No more child: I'm killing myself.")
					killMe
				}
			]
		}
		else {
			println("As leaf: Waiting awhile before commiting a suicide.")
			in(10000) [
				println("As leaf: I'm killing myself.")
				killMe
			]
		}
	}
	
	on Destroy {
		if (isRoot) {
			emit(new HolarchyDestroyed)
		}
	}
		
}
